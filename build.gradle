buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:0.6"
        classpath "com.sarhanm:gradle-versioner:1.0.45.master.6a0e39b"
    }
}

apply plugin: "java-gradle-plugin"
apply plugin: "com.jfrog.bintray"
apply plugin: "maven-publish"
apply plugin: "pmd"
apply plugin: "findbugs"
apply plugin: "jdepend"
apply plugin: "jacoco"

import com.sarhanm.versioner.VersionerOptions

project.extensions.create('versioner', VersionerOptions)
versioner {
    omitBranchMetadata = true
}
apply plugin: "com.sarhanm.versioner"

group = "amnl"
project.description = "Add code complexity reports to your Gradle build using Lizard (written in Python)."

sourceCompatibility = 1.6
targetCompatibility = 1.6

task javadocJar(type: Jar) {
    classifier = "javadoc"
    from javadoc
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = "sources"
}

def pomConfig = {
    licenses {
        license {
            name "MIT License"
            url "https://raw.githubusercontent.com/itavero/gradle-pylizard/master/LICENSE"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "amnl"
            name "Arno Moonen"
            email "info@arnom.nl"
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar

            pom.withXml {
                def root = asNode()
                root.appendNode("description", project.description)
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = bintrayUser
    key = bintrayKey
    publications = ["mavenJava"]
    dryRun = false
    publish = false
    pkg {
        repo = "gradle"
        name = rootProject.name
        desc = project.description
        licenses = ["MIT"]
        websiteUrl = "https://github.com/itavero/gradle-pylizard"
        issueTrackerUrl = "https://github.com/itavero/gradle-pylizard/issues"
        vcsUrl = "https://github.com/itavero/gradle-pylizard.git"
        labels = ["gradle", "lizard", "python", "SCA", "analysis"]
        version {
            name = project.version
            attributes = ['gradle-plugin': 'amnl.pylizard:amnl:gradle-pylizard']
        }
    }
}

task wrapper(type: Wrapper, description: "Gradle Wrapper task") {
    gradleVersion = "2.1"
}

pmd {
    toolVersion = "5.1.3"
    ruleSets = [
            "java-basic",
            "java-braces",
            "java-clone",
            "java-codesize",
            "java-design",
            "java-empty",
            "java-finalizers",
            "java-imports",
            "java-naming",
            "java-optimizations",
            "java-strictexception",
            "java-strings",
            "java-sunsecure",
            "java-typeresolution",
            "java-unnecessary",
            "java-unusedcode"
    ]
    ignoreFailures = true
    targetJdk = TargetJdk.VERSION_1_5
}

findbugs {
    ignoreFailures = true
}

jdepend {
    ignoreFailures = true
}

repositories {
    jcenter()
}

dependencies {
    testCompile "org.mockito:mockito-core:1.+"
}